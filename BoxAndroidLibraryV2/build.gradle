buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:0.5.+'
	}
}

apply plugin: 'android-library'
apply plugin: 'maven'

group = 'com.box.android'
version = '1.0-SNAPSHOT'

repositories {
	mavenLocal()
	mavenCentral()
}

configurations {
	archives {
		extendsFrom configurations.default
	}
}

uploadArchives {
	configuration = configurations.archives
	repositories.mavenDeployer {
		repository(url: releaseRepo) {
			authentication(userName: username,
				password: password)
		}
		snapshotRepository(url: snapshotRepo) {
			authentication(userName: username,
				password: password)
		}

		pom.project {
			name 'Box Android SDK v2'
			artifactId 'boxandroidlibv2'
			packaging 'aar'
			description 'Android SDK for Box v2'
			url 'https://github.com/box/box-android-sdk-v2'

			scm {
				url 'scm:git@github.com:box/box-android-sdk-v2.git'
				connection 'scm:git@github.com:box/box-android-sdk-v2.git'
				developerConnection 'scm:git@github.com:box/box-android-sdk-v2.git'
			}

			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}

			developers {
				developer {
					id 'gcurtis'
					name 'Greg Curtis'
					email 'gcurtis@box.com'
				}
			}
		}
	}
}

dependencies {
	compile 'com.box.boxjavalibv2:BoxJavaLibraryV2:1.0'
	compile 'com.fasterxml.jackson.core:jackson-annotations:2.2.2'
	compile 'com.fasterxml.jackson.core:jackson-core:2.2.2'
	compile 'com.fasterxml.jackson.core:jackson-databind:2.2.2'
	compile 'com.google.android:support-v4:r13'
	compile 'commons-codec:commons-codec:1.8'
	compile 'commons-io:commons-io:2.4'
	compile 'commons-lang:commons-lang:2.6'
	compile 'org.apache.httpcomponents:httpclient-cache:4.2.5'
	compile 'org.apache.httpcomponents:httpcore:4.2.4'
	compile 'org.apache.httpcomponents:httpmime:4.2.5'
}

android {
	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			res.srcDirs = ['res']
		}
	}
	buildToolsVersion "17.0"
	compileSdkVersion 17
}
